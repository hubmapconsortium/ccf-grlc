#+ summary: A list of unmapped AS, CT, and B terms in HRA

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT DISTINCT ?asctb_table ?record_number ?as ?as_label ?ct ?ct_label ?biomarker ?biomarker_label ?reference
FROM HRA:
WHERE {
  ?descriptor a ccf:CellMarkerDescriptor ;
    ccf:primary_anatomical_structure ?as ;
    ccf:primary_cell_type ?ct ;
    ccf:biomarker ?biomarker ;
    ccf:source_record ?record ;
    dct:references ?reference .

  ?as rdfs:label ?as_label .
  ?ct rdfs:label ?ct_label .
  ?biomarker rdfs:label ?biomarker_label .

  BIND(REPLACE(REPLACE(REPLACE(STR(?ct), 'http://purl.obolibrary.org/obo/CL_', 'CL:'),'http://purl.obolibrary.org/obo/PCL_', 'PCL:'), 'https://purl.org/ccf/ASCTB-TEMP_', 'ASCTB-TEMP:') as ?cell_id)
  BIND(STRAFTER(STR(?record), '#R') as ?record_number)
  BIND(REPLACE(REPLACE(STR(?biomarker), 'http://identifiers.org/hgnc/', 'HGNC:'), 'https://purl.org/ccf/ASCTB-TEMP_', 'ASCTB-TEMP:') as ?hgnc_id)
  BIND(STRBEFORE(REPLACE(STRBEFORE(STR(?descriptor), '#'), 'https://purl.humanatlas.io/asct-b/', ''), '/') as ?asctb_table)
  
  FILTER(STRSTARTS(STR(?as), STR(ASCTB-TEMP:)) || STRSTARTS(STR(?ct), STR(ASCTB-TEMP:)) || STRSTARTS(STR(?biomarker), STR(ASCTB-TEMP:)))
}
ORDER BY ?asctb_table ?record_number
