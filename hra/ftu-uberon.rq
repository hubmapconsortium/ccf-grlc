#+ summary: get FTU uberon info

PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>
PREFIX LOD: <https://lod.humanatlas.io>

SELECT DISTINCT ?ftu_digital_object ?ftu_label ?ftu_iri ?organ_label ?organ_iri ?image_url
FROM HRA:
WHERE {
  ?ftu_illustration a ccf:FtuIllustration ;
  	a ?ftu_iri ;
  	ccf:ccf_located_in ?organ_id ;
    ccf:image_file [
      ccf:file_format ?format ;
      ccf:file_url ?image_url
    ] .

  HRA: prov:hadMember ?versioned_ftu .

  BIND(IRI(REPLACE(STR(?organ_id), 'UBERON:', STR(UBERON:))) as ?organ_iri)
  BIND(IRI(REPLACE(STR(?ftu_illustration), "#primary", "")) as ?ftu_digital_object)

  ?ftu_iri rdfs:label ?ftu_label .
  ?organ_iri rdfs:label ?organ_label .
  
  FILTER(?format = "image/png") # or "image/svg+xml"
  FILTER(STRSTARTS(STR(?ftu_iri), STR(obo:)))
  FILTER(STRSTARTS(STR(?versioned_ftu), STR(?ftu_digital_object)))
}
