PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX ccf1: <http://purl.org/ccf/latest/ccf.owl#>
PREFIX HRAbody: <https://purl.humanatlas.io/graph/hra-ccf-body#>
PREFIX DSGraphs: <https://purl.humanatlas.io/collection/ds-graphs>
PREFIX DSGraphsExtra: <https://purl.humanatlas.io/graph/ds-graphs-enrichments>
PREFIX HRA: <https://purl.humanatlas.io/graph/hra-ccf-patches/draft>

SELECT DISTINCT ?source ?target 
  ?x_scaling ?y_scaling ?z_scaling
  ?x_rotation ?y_rotation ?z_rotation
  ?x_translation ?y_translation ?z_translation ?translation_units
FROM HRA:
WHERE {
  []
    rdf:type ccf:SpatialPlacement ;
    ccf:placement_for ?source ;
    ccf:placement_relative_to ?target ;
    ccf:x_scaling ?tmp_x_scaling ;
    ccf:y_scaling ?tmp_y_scaling ;
    ccf:z_scaling ?tmp_z_scaling ;
    ccf:x_rotation ?tmp_x_rotation ;
    ccf:y_rotation ?tmp_y_rotation ;
    ccf:z_rotation ?tmp_z_rotation ;
    ccf:x_translation ?tmp_x_translation ;
    ccf:y_translation ?tmp_y_translation ;
    ccf:z_translation ?tmp_z_translation ;
    ccf:translation_unit ?translation_units .

    FILTER(?target = ccf1:VHBothSexes || ?target = HRAbody:VHBothSexes || 
      !(?source IN (ccf1:VHFemale, ccf1:VHMale, HRAbody:VHFemale, HRAbody:VHMale)))

    BIND(xsd:decimal(?tmp_x_scaling) as ?x_scaling)
    BIND(xsd:decimal(?tmp_y_scaling) as ?y_scaling)
    BIND(xsd:decimal(?tmp_z_scaling) as ?z_scaling)

    BIND(xsd:decimal(?tmp_x_rotation) as ?x_rotation)
    BIND(xsd:decimal(?tmp_y_rotation) as ?y_rotation)
    BIND(xsd:decimal(?tmp_z_rotation) as ?z_rotation)

    BIND(xsd:decimal(?tmp_x_translation) as ?x_translation)
    BIND(xsd:decimal(?tmp_y_translation) as ?y_translation)
    BIND(xsd:decimal(?tmp_z_translation) as ?z_translation)
}
ORDER BY ?source