#+ summary: atlas cell type level mapping

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra-api>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>

SELECT DISTINCT ?cell_label ?cell_id ?level_1_cell_id ?level_1_cell_label
FROM HRA:
FROM <https://purl.humanatlas.io/vocab/cl>
FROM <https://purl.humanatlas.io/vocab/pcl>
WHERE {
  # Select cell types to map
  GRAPH HRApop: {
    [] ccf:has_cell_summary [
      ccf:has_cell_summary_row [
        ccf:cell_id ?cell_id ;
      ]
    ] .
    FILTER(!STRSTARTS(STR(?cell_id), 'https://purl.org/ccf/ASCTB-TEMP'))
  }
  ?cell_id rdfs:label ?cell_label .
  
  # Match to upper level cell types
  OPTIONAL {
    # https://github.com/obophenotype/cell-ontology/blob/master/src/templates/general_cell_types_upper_slim.csv
    VALUES (?l1_cell_id) {
      (CL:0000540) # neuron
      (CL:0000125) # glial cell
      (CL:0000066) # epithelial cell
      (CL:0000152) # exocrine cell
      (CL:0000057) # fibroblast
      (CL:0000235) # macrophage
      (CL:0000084) # T cell
      (CL:0000236) # B cell
      (CL:0000623) # natural killer cell
      (CL:0000451) # dendritic cell
      (CL:0000192) # smooth muscle cell
      (CL:0000746) # cardiac muscle cell
      (CL:0000188) # cardiac muscle cell
      (CL:0000163) # endocrine cell
      (CL:0000148) # melanocyte
      (CL:0009004) # retinal cell
      (CL:0000349) # extraembryonic cell
      (CL:0000081) # blood cell
      (CL:0000576) # monocyte
      (CL:0000039) # germ line cell
      (CL:0000136) # fat cell
      (CL:0000499) # stromal cell
      (CL:0001035) # bone cell
      (CL:0000034) # stem cell
    }
    ?cell_id (rdfs:subClassOf|rdf:type)* ?l1_cell_id .
    ?l1_cell_id rdfs:label ?l1_cell_label .
  }

  BIND(COALESCE(?l1_cell_id, CL:0000000) as ?level_1_cell_id)
  BIND(COALESCE(?l1_cell_label, 'unknown cell') as ?level_1_cell_label)
}
ORDER BY ?cell_id
