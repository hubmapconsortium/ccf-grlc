#+ summary: get HRApop datasets and cells with AS collisions that could have an FTU in it

PREFIX hint: <http://www.bigdata.com/queryHints#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX HRA_API: <https://purl.humanatlas.io/collection/hra-api>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>

SELECT DISTINCT ?dataset ?organ_id ?ftu_digital_object ?image_url ?organ_label ?ftu_label ?ftu_part_label ?organ_iri ?ftu_iri ?ftu_part_iri ?cell_type_count ?cell_percentage
WHERE {
  {
    SELECT DISTINCT *
    WHERE {
      GRAPH HRA: {
        {
          ?ftu_illustration a ccf:FtuIllustration ;
            a ?ftu_iri ;
            ccf:ccf_located_in ?organ_iri ;
            ccf:illustration_node [ a ?ftu_part_iri ] ;
            ccf:image_file [
              ccf:file_format ?format ;
              ccf:file_url ?image_url
            ] .
          BIND(IRI(REPLACE(STR(?ftu_illustration), "#primary", "")) as ?ftu_digital_object)
          
          ?ftu_part_iri rdfs:label ?ftu_part_label .
          ?ftu_iri rdfs:label ?ftu_label .
          ?organ_iri rdfs:label ?organ_label .

          BIND(IRI(REPLACE(STR(?organ_iri), STR(UBERON:), 'UBERON:')) as ?organ_id)
          FILTER(?format = "image/png") # or "image/svg+xml"
          FILTER(STRSTARTS(STR(?ftu_iri), STR(obo:)))
          FILTER(STRSTARTS(STR(?ftu_part_iri), STR(obo:)))
        }

        ?parent_as ccf:ccf_part_of ?organ_iri .
        ?ftu_iri ccf:ccf_part_of* ?parent_as .
        ?ftu_part_iri ccf:ccf_located_in|ccf:ccf_part_of+ ?ftu_iri .

        OPTIONAL {
          ?other_as ccf:ccf_part_of ?organ_iri .
          ?ftu_part_iri ccf:ccf_located_in|ccf:ccf_part_of+ ?other_as .
          FILTER(?other_as != ?organ_iri && ?other_as != ?ftu_iri && ?other_as != ?parent_as)
        }
        FILTER(!BOUND(?other_as))
      }
    }
  }

  {
    SELECT DISTINCT *
    WHERE {
      GRAPH HRApop: {
        {
          ?sample ccf:comes_from ?donor .
          ?sample ccf:has_registration_location ?rui_location .
          ?sample ccf:generates_dataset ?dataset .
        } UNION {
          ?block ccf:comes_from ?donor .
          ?block ccf:subdivided_into_sections ?sample .
          ?block ccf:has_registration_location ?rui_location .
          ?sample ccf:generates_dataset ?dataset .
        }

        ?rui_location ccf:has_collision_summary [
          ccf:has_collision_item [
            ccf:has_reference_organ ?refOrgan ;
          ]
        ] .
      }
      GRAPH HRA_API: {
        ?refOrgan owl:sameAs* [
          ccf:representation_of ?organ_iri ;
        ] .
      }
      GRAPH HRA: {
        [] a ccf:FtuIllustration ;
          ccf:representation_of ?ftu_iri ;
          ccf:ccf_located_in ?organ_iri .
      }
    }
  }

  GRAPH HRApop: {
    ?dataset ccf:has_cell_summary [
      ccf:has_cell_summary_row [
        ccf:cell_id ?ftu_part_iri ;
        ccf:cell_count ?cell_type_count ;
        ccf:percentage_of_total ?cell_pct ;
      ] ;
    ] .
    BIND(xsd:decimal(?cell_pct) as ?cell_percentage)
  }
}
ORDER BY ?ftu_digital_object ?dataset DESC(?cell_type_count)
