PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX ctpop: <https://cns-iu.github.io/hra-cell-type-populations-supporting-information/data/enriched_rui_locations.jsonld#graph>

SELECT DISTINCT ?organ ?organ_label ?dataset ?label ?link ?description
FROM <https://purl.org/ccf/releases/2.2.1/ccf.owl>
FROM ctpop:
WHERE {
  # List of query organ iris
  VALUES ?organ { <http://purl.obolibrary.org/obo/UBERON_0002113> }

  # Get all AS part of ?organ
  {
    ?child_as ccf:ccf_part_of* ?organ .
    ?organ rdfs:label ?organ_label .
    ?child_as rdfs:label ?child_label .
  }
  UNION
  { # Make ?organ selectable as well
    BIND(?organ as ?child_as)
  }

  # Get ?ref_organ that represent something part of ?organ
  {
    ?ref_organ ccf:has_reference_organ ?ref_organ .
    ?ref_organ ccf:representation_of ?child_as .
  }

  # Get metadata about ?datasets placed relative to ?ref_organ
  {
    ?rui_location a ccf:SpatialEntity .
    ?dataset a ccf:Dataset .
    ?sample ccf:generates_dataset ?dataset .
    ?sample ccf:has_registration_location ?rui_location .

    ?placement ccf:placement_for ?rui_location .
    ?placement ccf:placement_relative_to ?ref_organ .

    ?dataset rdfs:label ?label .
    ?dataset ccf:url ?link .
    ?dataset rdfs:comment ?description .
  }
}
