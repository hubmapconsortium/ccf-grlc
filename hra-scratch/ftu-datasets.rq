#+ summary: Datasets by FTU
#+ description: Finds relevant Datasets for each FTU

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX ctpop: <https://cns-iu.github.io/hra-cell-type-populations-supporting-information/data/enriched_rui_locations.jsonld#graph>
PREFIX iftu: <https://hubmapconsortium.github.io/hra-ui/apps/ftu-ui/assets/TEMP/2d-ftu-illustrations.jsonld>

SELECT DISTINCT ?ftu ?organ_label ?dataset ?label ?link ?description
FROM <https://purl.humanatlas.io/graph/ccf>
FROM ctpop:
FROM iftu:
WHERE {
  {
    ?ftu ccf:organ_id ?organ .
  } UNION {
    # Workaround for prostate
    VALUES (?ftu ?organ) {
      ( <https://purl.humanatlas.io/2d-ftu/prostate-prostate-glandular-acinus> <http://purl.obolibrary.org/obo/UBERON_0000079> )
    }
  }

  # List of query organ iris
  {
    ?ftu rdfs:label ?ftu_label .
    ?ftu ccf:representation_of ?ftu_term .
  }
  ?organ rdfs:label ?organ_label .

  # Get all AS part of ?organ
  {
    ?child_as ccf:ccf_part_of* ?organ .
    ?child_as rdfs:label ?child_label .
  }

  # Get ?ref_organ that represent something part of ?organ
  {
    ?ref_organ ccf:has_reference_organ ?ref_organ .
    ?ref_organ ccf:representation_of ?child_as .
  }

  # Get ?datasets placed relative to ?ref_organ
  {
    ?rui_location a ccf:SpatialEntity .
    ?dataset a ccf:Dataset .
    ?sample ccf:generates_dataset ?dataset .
    ?sample ccf:has_registration_location ?rui_location .

    ?placement ccf:placement_for ?rui_location .
    ?placement ccf:placement_relative_to ?ref_organ .
  }

  # Get ?dataset metadata
  {
    ?dataset rdfs:label ?label .
    ?dataset ccf:url ?link .
    ?dataset rdfs:comment ?description .
  }
}
